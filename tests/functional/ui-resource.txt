*** Settings ***
Documentation   Common variables and keywords
Library         Selenium2Library  0  implicit_wait=10    # Set explicit wait to 0 and implicit wait to 10 sec


*** Keywords ***

Open Browser to Login Page
    Open Browser  ${LOGIN_PAGE}  ${BROWSER}
    Maximize Browser Window

Go to Welcome Page
    Go to  ${WELCOME_PAGE}
    Title should be  multiproject - home

Logout
    Click Link  id=signOut

Create new project  [Arguments]  ${projname}
    [Documentation]  Attempt to create a new project, without verification of the result. 
    ...              Success is not checked, so that this kw can be used for failing case too.
    Click Link  Create new project >
    Check that title contains  Create a New Project - home
    Input text  prj_long_name  ${projname}
    Input text  prj_description  This is a dummy test project
    Click button  id=submit_create_form

Remove project
    [Arguments]  ${proj_name}
    Go to        ${SERVER}/${proj_name}/admin/general/system
    Click element   name=remove
    Click button   Confirm

Create user
    [Documentation]  Attempts to create local user
    [Arguments]  ${username}  ${email}
    Input text  first  ${username}
    Input text  last  ${username}
    Input text  email  ${email}
    Click element  name=create

Create group
    [Documentation]  Attempts to create group to permissions with certain permission
    [Arguments]  ${group_name}  ${permission}
    Click element  id=new_group_button
    Input text  group_name  ${group_name}
    Click element  id=${permission}
    Click element  name=create_button

Add user to group
    [Documentation]  Attempts to add user to group
    [Arguments]  ${group_name}  ${username}
    Input text  id=${group_name}_member  ${username}.${username}
    Click element  id=${group_name}_submit_btn

Activate local user
    [Documentation]  Attempts to activate local user and set password
    [Arguments]  ${username}  ${password}
    Select From List  id=states  2
    Input Text  id=expires  ""
    Input Password  name=password  ${password}
    Input Password  name=confirmpw  ${password}
    Click element  id=notify
    Checkbox Should Not Be Selected  id=notify
    Click element  name=save

Check that title contains
    [Documentation]  Check thath title contains wanted string.
    [Arguments]  ${content}
    ${real_title}=  Get title
    Should contain  ${real_title}  ${content}


Check that element contains
    [Documentation]  Wrapper around "Selenium2Library.Element should contain"
    ...   keyword, adding retries.
    [Arguments]  ${locator}  ${content}
    Wait Until Keyword Succeeds  1 min  5 sec  Element should contain  ${locator}  ${content}

Create ticket
    [Documentation]  Creates ticket for a project
    [Arguments]  ${summary}  ${description}
    Input text  id=field-summary  ${summary}
    Input text  id=field-description  ${description}
    Click element  name=submit


# vim:sw=4:ts=4
